# vim:set ft=zsh foldmethod=marker:

function prompt_aereal_setup() { # {{{
  if ! (which add-zsh-hook > /dev/null); then
    autoload -Uz add-zsh-hooks
  fi
  add-zsh-hook precmd aereal_update_prompt
} # }}}

function detect_ruby_version() { # {{{
  which rbenv > /dev/null 2>&1
  if [ $? -ne 0 ]; then
    return
  fi
  echo "ruby-$(rbenv version-name)"
} # }}}

function detect_perl_version() { # {{{
  which plenv > /dev/null 2>&1
  if [ $? -ne 0 ]; then
    return
  fi
  echo "perl-$(plenv version-name)"
} # }}}

function git_repository_relative_dir() { # {{{
  if ! git rev-parse --is-inside-work-tree 1>/dev/null 2>&1; then
    return
  fi

  local project repository_dir prefix
  repository_dir=${${$(git rev-parse --show-cdup 2>/dev/null):-"."}:a}
  project=${repository_dir:t}
  prefix=$(git rev-parse --show-prefix 2>/dev/null)
  echo "${project}${prefix:+":$prefix"}"
} # }}}

function aereal_update_prompt() { # {{{
  local \
    ruby_version \
    perl_version \
    current_ts \
    login_info \
    additional_info \
    current_user \
    current_host \
    current_working_directory \
    git_info \
    top_line \
    command_line

  ruby_version="%{${fg[red]}%}$(detect_ruby_version)%{${reset_color}%}"
  perl_version="%{${fg[blue]}%}$(detect_perl_version)%{${reset_color}%}"

  git_info=$(git_info)
  current_ts="%D{%Y-%m-%d} %*"
  git_info=${git_info:+"[$git_info] "}

  current_user="%{${fg[magenta]}%}%n%{${reset_color}%}"
  current_host="%{${fg[cyan]}%}%M%{${reset_color}%}"
  login_info="${current_user} @ ${current_host}"

  if git rev-parse --is-inside-work-tree 1>/dev/null 2>&1; then
    current_working_directory="%{${fg[yellow]}%}$(git_repository_relative_dir)%{${reset_color}%}"
  else
    current_working_directory="%{${fg[magenta]}%}%~%{${reset_color}%}"
  fi

  additional_info="${ruby_version} ${perl_version}"

  top_line="${git_info}${additional_info:+"(${additional_info})"}"

  ok_prompt=" %{${fg[yellow]}%}✘╹◡╹✘%{${reset_color}%} < "
  ng_prompt=" %{${fg[red]}%}✘>_<✘%{${reset_color}%} < "
  command_line="%(?,$ok_prompt,$ng_prompt)"

  PROMPT="$(echo -n "${top_line}\n${command_line}")"
  RPROMPT=${current_working_directory:+"[$current_working_directory]"}
}

prompt_aereal_setup "$@"
