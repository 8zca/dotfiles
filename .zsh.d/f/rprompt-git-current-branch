# vim:set ft=zsh:

function rprompt-git-current-branch {
  # http://d.hatena.ne.jp/uasi/20091025/1256458798
  local name st color gitdir action annotation
  if [[ "$PWD" =~ '\.git(/.*)?$' ]]; then
    return
  fi
  name=`git branch 2> /dev/null | grep '^\*' | cut -b 3-`
  if [[ -z $name ]]; then
    return
  fi

  gitdir=`git rev-parse --git-dir 2> /dev/null`
  action=`VCS_INFO_git_getaction "$gitdir"` && action="($action)"

  st=`git status --short --porcelain -b 2> /dev/null`

  if [[ -n `echo "$st" | grep "^[UMADRC]"` ]]; then
    # Staged
    annotation="$annotation*"
    color=%F{yellow}
  fi

  if [[ -n `echo "$st" | grep "??"` ]]; then
    # Untracked
    annotation="$annotation?"
    color=%F{yellow}
  fi

  if [[ -n `echo "$st" | grep "^[ ][MD]"` ]]; then
    # Changed, but not staged
    annotation="$annotation!"
    color=%F{red}
  fi

  if [[ -n `echo "$st" | grep "^##.\+]$"` ]]; then
    name="+$name"
  fi

  if [[ -z "$color" ]]; then
    color=%F{green}
  fi

  echo "$color$name$action$annotation%f%b"
}

