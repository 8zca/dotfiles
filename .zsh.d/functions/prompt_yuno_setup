function prompt_yuno_setup() { # {{{
  if ! (whence add-zsh-hook >/dev/null); then
    autoload -Uz add-zsh-hooks
  fi
  add-zsh-hook precmd __prompt_yuno_update

  # zsh-git-prompt
  export ZSH_GIT_PROMPT_ROOT="$ZSH_HOME/plugins/zsh-git-prompt"
  if [[ -f "$ZSH_GIT_PROMPT_ROOT/zshrc.sh" ]]; then
    export ZSH_THEME_GIT_PROMPT_CACHE=1
    source "$ZSH_GIT_PROMPT_ROOT/zshrc.sh"
  fi
  if [[ -f "$ZSH_GIT_PROMPT_ROOT/dist/build/gitstatus/gitstatus" ]]; then
    export GIT_PROMPT_EXECUTABLE='haskell'
  fi

  export ZSH_THEME_GIT_PROMPT_PREFIX=""
  export ZSH_THEME_GIT_PROMPT_SUFFIX=""
  export ZSH_THEME_GIT_PROMPT_SEPARATOR="|"
  export ZSH_THEME_GIT_PROMPT_BRANCH="%{$fg_bold[magenta]%}"
  export ZSH_THEME_GIT_PROMPT_STAGED="%{$fg[blue]%}%{.%G%}"
  export ZSH_THEME_GIT_PROMPT_CONFLICTS="%{$fg[red]%}%{!%G%}"
  export ZSH_THEME_GIT_PROMPT_CHANGED="%{$fg[yellow]%}%{!%G%}"
  export ZSH_THEME_GIT_PROMPT_BEHIND="%{<%G%}"
  export ZSH_THEME_GIT_PROMPT_AHEAD="%{$fg[green]%}%{>%G%}"
  export ZSH_THEME_GIT_PROMPT_UNTRACKED="%{?%G%}"
  export ZSH_THEME_GIT_PROMPT_CLEAN="%{$fg_bold[green]%}%{*%G%}"

  autoload -Uz llenv_version_origin
} # }}}

function __prompt_yuno_update() { # {{{
  # components {{{
  local ok_yuno="%F{yellow}✘╹◡╹✘%f"
  local bad_yuno="%F{red}✘>﹏<✘%f"
  local command_line="%(?.${ok_yuno}.${bad_yuno}) < "
  local working_directory="%F{cyan}%~%f"
  local accessory_info="${working_directory}"

  # Perl version {{{
  local perl_version_source=$(llenv_version_origin plenv)
  case "$perl_version_source" in
    "local"|shell)
      accessory_info="%F{blue}Perl-$(plenv version-name)%f ${accessory_info}"
      ;;
    *)
      ;;
  esac
  # }}}
  # Ruby version {{{
  local ruby_version_source=$(llenv_version_origin rbenv)
  case "$ruby_version_source" in
    "local"|shell)
      accessory_info="%F{red}Ruby-$(rbenv version-name)%f ${accessory_info}"
      ;;
    *)
      ;;
  esac
  # }}}
  # }}}

  # join by newline
  local -a left_prompt_lines
  left_prompt_lines=(
    ${accessory_info}
    ${command_line}
  )

  # join by whitespace
  local -a right_prompt_components
  right_prompt_components=(
    $(git_super_status)
  )

  PROMPT="${(F)left_prompt_lines}"
  RPROMPT="${(j: :)right_prompt_components}"
} # }}}

prompt_yuno_setup "$@"

# vim:set ft=zsh foldmethod=marker:
